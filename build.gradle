import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'project-report'
    id 'idea'
    id "io.freefair.lombok" version "${lombok_plugin_version}" apply false
    id "org.liquibase.gradle" version "${liquibase_plugin_version}" apply false
    id 'org.springframework.boot' version "${spring_boot_version}" apply false
    id 'com.google.cloud.tools.jib' version "${jib_plugin_version}" apply false
}

allprojects {
    group = "com.atguigu.gulimall"
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url 'https://maven.aliyun.com/repository/spring'
        }
        maven {
            url 'https://maven.aliyun.com/repository/spring-plugin'
        }
        maven {
            url 'https://maven.aliyun.com/repository/gradle-plugin'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
        mavenCentral()
    }

    wrapper {
        gradleVersion = "6.5"
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'
    apply plugin: 'idea'
    apply plugin: 'io.freefair.lombok'
    apply plugin: "org.liquibase.gradle"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.google.cloud.tools.jib'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    assert System.properties["java.specification.version"] == "1.8" || "11" || "12" || "13" || "14"

    if (project.name == 'gulimall-common') {

        apply plugin: 'java-test-fixtures'

        bootJar {
            enabled = false
        }
        jar {
            enabled = true
        }
    }

    if (project.name.startsWith("gulimall-")) {
        springBoot {
            def pkgNameParts = project.name.split('-', 2)
            def pkgName = "com.atguigu.${pkgNameParts[0]}.${pkgNameParts[1].split('-').collect { it.capitalize() }.join("").uncapitalize()}"
            def appName = "${project.name.split("-").collect { it.capitalize() }.join("")}Application"
            mainClassName = "${pkgName}.${appName}"
        }
    }

    dependencies {
        // 导入依赖BOM文件
        implementation platform(SpringBootPlugin.BOM_COORDINATES)
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}")
        implementation platform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:${spring_cloud_alibaba_version}")

        if (project.name != 'gulimall-common') {
            implementation(project(':gulimall-common'))
            testImplementation(testFixtures(project(':gulimall-common')))
        } else {
            testFixturesImplementation platform(SpringBootPlugin.BOM_COORDINATES)
            testFixturesImplementation 'org.springframework.boot:spring-boot-starter-test'
            testFixturesImplementation 'com.fasterxml.jackson.core:jackson-databind'
            testFixturesImplementation "com.alibaba:fastjson:${alibaba_fastjson_version}"
            testFixturesCompileOnly 'javax.servlet:javax.servlet-api'
            testFixturesImplementation "it.ozimov:embedded-redis:${embedded_redis_version}"
        }

        // Spring boot
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        // Spring Security
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.security:spring-security-test'
        implementation "io.jsonwebtoken:jjwt-api:${jsonwebtoken_version}"
        runtimeOnly "io.jsonwebtoken:jjwt-impl:${jsonwebtoken_version}",
            // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
            //'org.bouncycastle:bcprov-jdk15on:1.60',
            "io.jsonwebtoken:jjwt-jackson:${jsonwebtoken_version}" // or 'io.jsonwebtoken:jjwt-gson:0.11.2' for gson

        // Spring Mvc Web
        implementation ("org.springframework.boot:spring-boot-starter-web") {
            exclude module: "spring-boot-starter-tomcat"
        }
        implementation "org.springframework.boot:spring-boot-starter-undertow"

        // Spring cloud
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'

        // Spring session
        implementation 'org.springframework.session:spring-session-data-redis'

        // Spring cloud alibaba
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-sentinel'
        implementation 'com.alibaba.csp:sentinel-spring-webmvc-adapter'

        // SQL Database 数据库
        implementation "com.baomidou:mybatis-plus-boot-starter:${mybatis_plus_version}"
        implementation "mysql:mysql-connector-java:${mysql_driver_version}"
        implementation "com.h2database:h2"
        testImplementation "com.h2database:h2"

        // Liquibase 数据库版本管理
        implementation "org.liquibase:liquibase-core"
        liquibaseRuntime "org.liquibase:liquibase-core"
        liquibaseRuntime sourceSets.main.compileClasspath
        liquibaseRuntime "mysql:mysql-connector-java"
        liquibaseRuntime "com.h2database:h2"

        // Redis
        implementation ('org.springframework.boot:spring-boot-starter-data-redis') {
            exclude group: 'io.lettuce', module: 'lettuce-core'
        }
        implementation 'redis.clients:jedis'
        testImplementation "it.ozimov:embedded-redis:${embedded_redis_version}"

        implementation 'javax.validation:validation-api'
        implementation 'org.hibernate.validator:hibernate-validator'

        compileOnly 'javax.servlet:javax.servlet-api'

        implementation 'org.apache.httpcomponents:fluent-hc:4.5.12'
        implementation 'commons-lang:commons-lang:2.6'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'com.baomidou:mybatis-plus-boot-starter-test:3.4.0'
        //testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.1.3'

        implementation 'org.javers:javers-core:5.11.2'

    }

    ext {
        diffChangelogFile = "src/main/resources/config/liquibase/changelog/" + new Date().format("yyyyMMddHHmmss") + "_changelog.xml"
    }

    if (!project.hasProperty("runList")) {
        project.ext.runList = "main"
    }

    liquibase {
        activities {
            main {
                driver ""
                url ""
                username "gulimall"
                password ""
                changeLogFile "src/main/resources/config/liquibase/master.xml"
                defaultSchemaName ""
                logLevel "debug"
                classpath "src/main/resources/"
            }
            diffLog {
                driver ""
                url ""
                username "gulimall"
                password ""
                changeLogFile project.ext.diffChangelogFile
                referenceUrl "hibernate:spring:com.mycompany.myapp.domain?dialect=&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
                defaultSchemaName ""
                logLevel "debug"
                classpath "$buildDir/classes/java/main"
            }
        }
        runList = project.ext.runList
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

task listSubprojects() {
    subprojects.each { println(it.name) }
}
task listApps() {
    subprojects.each { println(it.springBoot.mainClassName) }
}
